]dinput
vin←{
    ⎕IO←1

    vals←0,(27⍴⍳9),1+⍳8
    weights←⌽1+(⍳8),9,⍳7
    checkDigit←(⎕D,'X')⌷⍨1+11|(weights↑⍨∘≢⊢)+.×vals⌷⍨∘⊂(⎕D,⎕A)∘⍳

    vin16←∊¯1⌽checkDigit,¯8∘↑,⍥⊂8∘↑
    vin17←9∘⊃≡∘checkDigit 8∘↑,¯8∘↑
    
    ∨⌿~⍵∊⎕D,⎕A~'IOQ':-1

    16≡≢⍵:vin16⍵
    17≡≢⍵:vin17⍵

    -1
}

]dinput
sortVersions←{
    ⎕IO←1
    (⊢⌷⍨∘⊂∘⍋((3∘↓,⍨2∘↑,'.'(≠⍎¨⍤⊆⊢)3∘⊃)'-'∘≠⊆⊢)¨),⊆⍵
}

]dinput
makeChange←{
    ⎕IO←1
    ⍵=0:1(≢⍺)⍴0
    ⍺≡⍬:0 0⍴⍬
    ⊃⍪/(⊢,(1↓⍺)makeChange⍵-(⊃⍺)×⊢)¨0,⍳⌊⍵÷⊃⍺
}

]dinput
partition←{
    ⍝ each will be ⍬ by default; if set to scalar, will be enclosed
    iDims steps start←1↓4↑(⊂⍬),,¨⊆⍺
    start←start-⎕IO
    ⎕IO←0

    d←≢⍴⍵
    prepad←{⍵,⍨(d-≢⍵)⍴⍺}
    dims←1 prepad iDims
    steps←1 prepad steps
    start←0 prepad start

    paddingDims←¯1+⌈2÷⍨dims
    strip←(-paddingDims)∘↓paddingDims∘↓
    res←strip(⊂⍤⊢⌺dims)⍵

    offsets←steps∘×¨⍳1+⌊(¯1+(⍴res)-start)÷steps
    iDims∘⍴¨,res[start∘+¨offsets]
 }
